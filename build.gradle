plugins {
  id 'java-library'
  id 'org.springframework.boot' apply(false)
  id 'io.spring.dependency-management'
  id 'com.bmuschko.docker-java-application' version '9.4.0' apply(false)
  id 'pl.allegro.tech.build.axion-release' version '1.15.1'
  id 'groovy'
  id "org.sonarqube" version '4.4.1.3373'
  id "jacoco"
  id "jvm-test-suite"
  id "jacoco-report-aggregation"
}
allprojects {
  group = "${projectGroup}"
  apply from: "${rootDir}/gradle/axion.gradle"

  java {
    sourceCompatibility = project.javaVersion
  }

  repositories {
    mavenCentral()
  }
  test {
    useJUnitPlatform()
  }
}
rootProject.afterEvaluate() {
  def currentVersionTask = project.tasks.named("currentVersion")
  currentVersionTask.configure {
    doNotTrackState("Walkarounds")
  }
}

subprojects {
  apply plugin: 'java-library'
  apply plugin: 'groovy'
  apply plugin: 'org.springframework.boot'
  apply plugin: 'io.spring.dependency-management'
  apply plugin: 'jacoco'
  apply plugin: 'jacoco-report-aggregation'
  apply plugin: 'org.sonarqube'
  apply from: "${rootDir}/gradle/test.gradle"

  bootJar.enabled = false
  jar.enabled = true

  dependencies {
    implementation platform('org.apache.groovy:groovy-bom:4.0.18')
    implementation 'org.apache.groovy:groovy'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'

    testImplementation platform("org.spockframework:spock-bom:2.4-M1-groovy-4.0")
    testImplementation "org.spockframework:spock-core"
    testImplementation "org.spockframework:spock-spring"
  }
}

