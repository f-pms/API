plugins {
  id 'java-library'
  id 'org.springframework.boot' apply(false)
  id 'io.spring.dependency-management'
  id 'com.bmuschko.docker-java-application' version '9.4.0' apply(false)
  id 'pl.allegro.tech.build.axion-release' version '1.15.1'
  id 'groovy'
}
allprojects {
  group = "${projectGroup}"
  apply from: "${rootDir}/gradle/axion.gradle"
  java {
    sourceCompatibility = project.javaVersion
  }

  repositories {
    mavenCentral()
  }
}
rootProject.afterEvaluate() {
  def currentVersionTask = project.tasks.named("currentVersion")
  currentVersionTask.configure {
    doNotTrackState("Walkarounds")
  }
}
subprojects {
  apply plugin: 'java-library'
  apply plugin: 'groovy'
  apply plugin: 'org.springframework.boot'
  apply plugin: 'io.spring.dependency-management'
  bootJar.enabled = false
  jar.enabled = true

  dependencies {
    implementation platform('org.apache.groovy:groovy-bom:4.0.18')
    implementation 'org.apache.groovy:groovy'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'

    testImplementation platform("org.spockframework:spock-bom:2.4-M1-groovy-4.0")
    testImplementation "org.spockframework:spock-core"
    testImplementation "org.spockframework:spock-spring"
  }
  test {
    useJUnitPlatform()
  }
}
